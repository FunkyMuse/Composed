buildscript {
    ext {
        compose_version = '1.0.0-beta03'
        kotlin_version = '1.4.30'
        lifecycle_version = '2.4.0-alpha01'
        coroutines_version = '1.4.3'
        turbine_version = '0.4.1'

        //compilation
        compileVersion = 30
        minVersion = 21
        verCode = 1
        verName = "1.0.0"
        testRunner = "androidx.test.runner.AndroidJUnitRunner"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0-alpha12'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

allprojects {
    group = "io.github.funkymuse"
}


subprojects {
    if (it.name != 'app') {
        //setup gradle for libraries
        apply plugin: 'com.android.library'
        apply plugin: 'kotlin-android'
    } else {
        apply plugin: 'com.android.application'
        apply plugin: 'kotlin-android'
    }

    dependencies {
        //test
        androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
        androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
        androidTestImplementation "app.cash.turbine:turbine:$turbine_version"

        testImplementation "app.cash.turbine:turbine:$turbine_version"
        testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
        testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"

        //lifecycle
        implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

        implementation "androidx.compose.ui:ui:$compose_version"
        implementation "androidx.compose.ui:ui-tooling:$compose_version"
        implementation "androidx.compose.material:material:$compose_version"
        implementation 'androidx.activity:activity-compose:1.3.0-alpha05'
    }

    android {
        compileSdkVersion compileVersion

        defaultConfig {
            minSdkVersion minVersion
            targetSdkVersion compileVersion
            versionCode verCode
            versionName verName
            testInstrumentationRunner testRunner
        }

        compileOptions {
            sourceCompatibility = 1.8
            targetCompatibility = 1.8
        }

        testOptions.unitTests.all {
            useJUnitPlatform()
            testLogging {
                events 'passed', 'failed', 'skipped', 'standardOut', 'standardError'
            }
        }

        kotlinOptions {
            jvmTarget = '1.8'
            useIR = true
        }

        buildFeatures {
            aidl = false
            renderScript = false
            resValues = false
            shaders = false
            buildConfig = false
            compose = true
        }

        testOptions.unitTests {
            includeAndroidResources = true
        }

        packagingOptions {
            exclude 'META-INF/AL2.0'
            exclude 'META-INF/LGPL2.1'
        }

    }
}